plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.14.0'
}

group = 'com.aterrizar'
version = '0.0.1-SNAPSHOT'
description = 'Course project for Aterrizar dot com'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

bootJar {
    mainClass = 'com.aterrizar.http.ServiceApplication'
    enabled = true
}

jar {
    enabled = false
}


configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':service')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.11'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'com.neovisionaries:nv-i18n:1.28'
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/http/src/main/resources/openapi/openapi.yaml"
    outputDir = "$buildDir/generated"
    apiPackage = "com.aterrizar.http.api"
    modelPackage = "com.aterrizar.http.dto"
    invokerPackage = "com.aterrizar.http.invoker"
    configOptions = Map.of(
            "useLombok", "true",
            "dateLibrary", "java8",
            "openApiNullable", "false",
            "deserializationLibrary", "jackson",
            "additionalModelTypeAnnotations", "@lombok.Builder",
            "useOptional", "true",
            "useRecords", "true",
            "hideGenerationTimestamp", "true"
    )

    additionalProperties = [
            "interfaceOnly"     : "true",
            "useTags"           : "true",
            "unhandledException": "true",
            "useSpringBoot3"    : "true",
            "useSpringDoc"      : "true"
    ]
}

sourceSets {
    main {
        java.srcDirs += ["$buildDir/generated/src/main/java"]
    }
}
compileJava.dependsOn tasks.openApiGenerate